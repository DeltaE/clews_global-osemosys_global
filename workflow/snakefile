import os
import yaml
from snakemake.utils import min_version
min_version("6.0")

# configuration

configfile: 'config/config.yaml'

# helper functions 

def get_otoole_data(otoole_config: str, var: str) -> list[str]:
    """Gets parameter/result files to be created"""

    assert var in ("param", "result", "set")

    with open(otoole_config) as f:
        otoole = yaml.safe_load(f)

    results = [x for x in otoole if otoole[x]["type"] == var]

    # no result calcualtions 
    missing = [
        "NewStorageCapacity",
        "NumberOfNewTechnologyUnits",
        "SalvageValueStorage",
        "StorageLevelDayTypeStart",
        "StorageLevelDayTypeFinish",
        "StorageLevelSeasonStart",
        "StorageLevelSeasonFinish",
        "StorageLevelYearStart",
        "StorageLevelYearFinish",
        "Trade",
        "DiscountedOperatingCost",
        "TotalDiscountedStorageCost",
        "ModelPeriodEmissions",
        "StorageLowerLimit",
    ]

    return [x for x in results if x not in missing]

# constants 

OTOOLE_YAML = "resources/otoole.yaml"
OTOOLE_PARAMS = get_otoole_data(OTOOLE_YAML, "param")
OTOOLE_RESULTS = get_otoole_data(OTOOLE_YAML, "result")

# rules

include: "rules/preprocess.smk"
include: "rules/model.smk"
include: "rules/postprocess.smk"
include: "rules/retrieve.smk"

# handlers 
        
onsuccess:
    print('Workflow finished successfully!')

    # Will fix this in next update so that preprocessing steps dont always need to be rerun
    [f.unlink() for f in Path('results', 'data').glob("*") if f.is_file()] 

onerror:
    print('An error occurred, please submit issue to '
        'https://github.com/OSeMOSYS/osemosys_global/issues')

# file creation check 

if not os.path.isdir(Path('results', 'data')):
    Path('results', 'data').mkdir(parents=True)

# target rules

rule all:
    message:
        'All rules executed successfully...' 
    input:
        expand('results/figs/{demand_figure}.png', demand_figure=["regression", "projection"]),
        expand('results/{scenario}/result_summaries/{result_summary}.csv', 
            scenario=config['scenario'], result_summary=result_summaries), 
        expand('results/{scenario}/figures/{result_figure}.html', 
            scenario=config['scenario'], result_figure = result_figures)

rule generate_input_data:
    message:
        "Generating input CSV data..."
    input:
        csv_files = expand('results/{scenario}/data/{csv}.csv', scenario=config['scenario'], csv=OTOOLE_PARAMS),

rule make_dag:
    message:
        'dag created successfully and saved as docs/dag.pdf'
    shell:
        'snakemake --dag all | dot -Tpdf > docs/dag.pdf'

# rule dashboard:
#     message: 
#         'Starting dashboard...'
#     shell:
#         'python workflow/scripts/osemosys_global/dashboard/app.py'

# cleaning rules

rule clean:
    message:
        'Reseting to defaults...'
    shell:
        'rm -rf results/*'

rule clean_data:
    shell:
        'rm -rf results/data/*'

rule clean_figures:
    shell:
        'rm -rf results/figs/*'
